buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'net.researchgate:gradle-release:2.3.4'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
    }
}

subprojects {
    repositories {
        mavenCentral()
        jcenter()
    }

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'net.researchgate.release'
    apply plugin: 'com.jfrog.bintray'

    group 'biz.k11i'

    compileJava {
        sourceCompatibility = JavaVersion.VERSION_1_7
        targetCompatibility = JavaVersion.VERSION_1_7
    }

    compileTestJava {
        sourceCompatibility = JavaVersion.VERSION_1_7
        targetCompatibility = JavaVersion.VERSION_1_7
    }

    javadoc {
        options.locale = 'en_US'
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives jar
        archives sourcesJar
        archives javadocJar
    }

    release {
        preTagCommitMessage = '[skip ci] [Gradle Release Plugin] - pre tag commit: '
        newVersionCommitMessage = '[skip ci] [Gradle Release Plugin] - new version commit: '
        tagTemplate = '$name-$version'
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }

    bintray {
        user = project.hasProperty('bintrayUser') ? bintrayUser : ''
        key = project.hasProperty('bintrayKey') ? bintrayKey : ''
        publications = ['maven']

        pkg {
            repo = 'maven'
            name = project.name
            desc = project.description
            userOrg = 'komiya-atsushi'
            licenses = ['Apache-2.0']

            websiteUrl = 'https://github.com/komiya-atsushi/xgboost-predictor-java'
            issueTrackerUrl = 'https://github.com/komiya-atsushi/xgboost-predictor-java/issues'
            vcsUrl = 'https://github.com/komiya-atsushi/xgboost-predictor-java.git'

            version {
                name = project.version
            }
        }
    }

    createReleaseTag.dependsOn bintrayUpload

    task checkJavaVersion << {
        if (!JavaVersion.current().isJava8Compatible()) {
            String message = "ERROR: Java 8 required but " +
                    JavaVersion.current() +
                    " found. Change your JAVA_HOME environment variable."
            throw new IllegalStateException(message)
        }
    }

    compileJava.dependsOn checkJavaVersion
}
